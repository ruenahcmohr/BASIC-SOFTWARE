5 DATA BLACK,BROWN,RED,ORANGE,YELLOW,GREEN,BLUE,VIOLET,GREY,WHITE
6 DATA BLACK,BROWN,RED,ORANGE,YELLOW,GREEN,BLUE,PURPLE,GRAY,WHITE
10 C = 0 : Q = 0
20 GOSUB 1000
25 IF Z = 5 THEN GOTO 6000
30 GOSUB 1500
40 IF Z = 1 THEN GOSUB 2000
50 IF Z = 2 THEN GOSUB 3000
60 IF Z = 3 THEN GOSUB 200
70 IF Z = 4 THEN GOSUB 5000
100 K = K - 1
110 IF K > 0 GOTO 40
120 GOSUB 2500
130 INPUT "ANY KEY TO RETURN TO MENU";Z$
140 GOTO 10
199 REM RANDOM COLOUR OR NUMBER QUESTION
200 GOSUB 8000 : IF (M > 0.5) GOTO 230
210 GOSUB 2000
220 RETURN
230 GOSUB 3000
240 RETURN
999 END
1000 GOSUB 7000
1010 PRINT " +--------------------------------+"
1015 PRINT " |  RUE MOHR COLOUR CODE BURNER   |"
1020 PRINT " |                                |"
1030 PRINT " |   1  Digits test               |"
1040 PRINT " |   2  Colours test              |"
1050 PRINT " |   3  Mix digit/colour test     |"
1060 PRINT " |   4  Resistor test             |"
1070 PRINT " |   5  Help                      |"
1080 PRINT " |                                |"
1085 PRINT " |(MAKE SURE YOUR CAPSLOCK IS ON!)|"
1090 PRINT " +--------------------------------+"
1100 PRINT
1110 INPUT "  What shall we do "; Z$
1120 Z = VAL(Z$)
1130 IF Z = 0 GOTO 1000
1140 IF Z > 5 GOTO 1000
1150 RETURN
1500 REM GET QUESTION COUNT
1505 GOSUB 7000
1510 INPUT "HOW MANY QUESTIONS WOULD YOU LIKE TO DO ";A$ : K = VAL(A$)
1520 IF K = 0 GOTO 1510
1530 RETURN
2000 REM DIGIT QUESTION
2010 Q = Q + 1 :GOSUB 8000 : M = INT(M*10)
2020 RESTORE 5 : FOR N = 0 TO M : READ S$ : NEXT N
2030 PRINT "WHAT IS THE VALUE OF THE COLOUR ";S$
2040 INPUT A$ : A = VAL(A$)
2050 IF A <> M GOTO 2100
2060 C = C + 1 : PRINT "  CORRECT! " : PRINT
2070 RETURN
2100 PRINT "NO, THE ANSWER IS ";M :PRINT
2110 RETURN
2500 REM PRINT SCORE
2510 PRINT "YOUR SCORE WAS: ";C/Q*100;" %"
2520 RETURN
3000 REM COLOUR QUESTION
3010 Q = Q + 1 :GOSUB 8000 : M = INT(M*10)
3020 RESTORE 5 : FOR N = 0 TO M : READ S$ : NEXT N
3025 RESTORE 6 : FOR N = 0 TO M : READ T$ : NEXT N
3030 PRINT "WHAT IS THE COLOUR OF THE VALUE ";M
3040 INPUT U$ 
3050 IF U$ = S$ THEN GOTO 3100
3060 IF U$ = T$ THEN GOTO 3100
3070 PRINT "NO, THE ANSWER IS ";S$ : PRINT
3080 RETURN
3100 C = C + 1 : PRINT "  CORRECT! " : PRINT
3110 RETURN
4000 REM RESISTOR COLOUR QUESTION
5000 REM RESISTOR VALUE QUESTION
5005 Q = Q + 1
5010 GOSUB 8000 : D = INT(M*9)+1
5020 GOSUB 8000 : E = INT(M*10)
5030 GOSUB 8000 : F = INT(M*10)
5040 GOSUB 8000 : G = INT(M*7)
5045 H = 0
5050 GOSUB 8000 : IF M > 0.5 THEN H = 1
5060 RESTORE 5 : FOR N = 0 TO D : READ Z$ : NEXT N 
5065 PRINT "  FIRST STRIPE: ";Z$
5070 RESTORE 5 : FOR N = 0 TO E : READ Z$ : NEXT N
5075 PRINT "  SECOND STRIPE: ";Z$
5076 PRINT "  THIRD STRIPE: ";
5077 IF (H = 1 ) THEN GOTO 5100
5080 RESTORE 5 : FOR N = 0 TO F : READ Z$ : NEXT N 
5085 PRINT Z$
5090 PRINT "  FOURTH STRIPE: ";
5100 RESTORE 5 : FOR N = 0 TO G : READ Z$ : NEXT N
5110 PRINT Z$
5200 INPUT "HOW MANY OHMS IS IT ";A$
5300 A = VAL(A$)
5400 B$ = MID$(STR$(D),2,1)+MID$(STR$(E),2,1)
5402 IF (H = 1) GOTO 5406
5404 B$ = B$ + MID$(STR$(F),2,1)
5406 B$ = B$ + "0000000000"
5410 B$ = MID$(B$,1,3+G-H)
5420 B = VAL(B$)
5430 IF (A = B) THEN GOTO 5500
5440 PRINT "NO, THE ANSWER IS ";B$ : PRINT
5450 RETURN
5500 C = C + 1 : PRINT "  CORRECT! " : PRINT
5510 RETURN
5999 RETURN
6000 GOSUB 7000
6010 PRINT " 0 BLACK"
6020 PRINT " 1 BROWN"
6030 PRINT " 2 RED"
6040 PRINT " 3 ORANGE"
6050 PRINT " 4 YELLOW"
6060 PRINT " 5 GREEN"
6070 PRINT " 6 BLUE"
6080 PRINT " 7 VIOLET"
6090 PRINT " 8 GREY"
6100 PRINT " 9 WHITE"
6110 PRINT 
6120 PRINT " FIRST STRIPES ARE DIGITS "
6130 PRINT " LAST STRIPE IS ZEROS TO ADD ON END OF VALUE"
6140 PRINT "  123 -> 1 2 000  "
6150 PRINT "  1234 -> 1 2 3 0000"
6160 PRINT
6170 INPUT "ANY KEY TO RETURN TO MENU"; Z$
6180 GOTO 10
7000 REM CLEARSCREEN ABSTRACTION
7005 REM PRINT CHR$(27);"[2J";
7010 CLS
7020 RETURN
8000 REM RANDOM ABSTRACTION
8005 REM M = RND(1000)/1001
8010 M = RND(1)
8020 RETURN



