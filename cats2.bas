5 S = 0 : W = 0 : V = 1 : H = 0 : L = 0
10 GOSUB 7000 : FOR N = 1 TO 50 : PRINT : NEXT N
20 GOSUB 6000
30 GOSUB 4000
40 FOR N = 1 TO 50
50 PRINT
60 FOR D = 1 TO 100
70 NEXT D
80 NEXT N
90 GOSUB 7000
100 PRINT "  YOUR MISSION, QUITE SIMPLY, IS TO COUNT THE CATS. "
105 PRINT
110 INPUT "ENTER YOUR NAME AND OR HANDLE FOR THE SCOREBOARD:"; N$
120 PRINT : PRINT "OK "; N$; " ARE YOU READY THEN? "
130 INPUT Z$
140 FOR Y = 1 TO LEN(N$) : GOSUB 8000 : NEXT Y
150 FOR Y = 1 TO LEN(Z$) : GOSUB 8000 : NEXT Y
200 GOSUB 7000
210 C = 0
215 GOSUB 1000 : REM WE NEED AT LEAST 1 CAT...
220 FOR N = 1 TO 3
230 GOSUB 8000 : IF M > 0.25 THEN GOTO 250
240 GOSUB 1000
250 PRINT
260 NEXT N
265 PRINT "PRETTY LAME EH? ";
270 GOSUB 2000
280 PRINT "OK ";N$;" NOW WE GONNA PLAY FOR REAL."
290 L = 1 : GOSUB 5000
499 REM FAST ROUND
500 FOR D = 1 TO 1500 : NEXT D
510 C = 0 : GOSUB 7000
515 PRINT "--------- LEVEL ";L;" ---------"
517 FOR N = 1 TO 80 : PRINT TAB(10);81-N : FOR D = 1 TO E:NEXT D:NEXT N
520 FOR N = 1 TO 50
530 GOSUB 8000 : IF M < T THEN GOTO 540
535 GOSUB 1000 : GOTO 590
540 PRINT
590 FOR D = 1 TO R : NEXT D : NEXT N
600 FOR N = 1 TO 80 : PRINT TAB(N/4);"." : FOR D = 1 TO E:NEXT D:NEXT N
610 GOSUB 2000
620 IF (S + W <> 2) THEN GOTO 630
625 PRINT "MY MISSION IS TO MAKE SURE YOU NEVER SEE LEVEL 10."
630 IF (C=U) THEN L = L + 1 
640 IF (C<>U) THEN L = L - 1 
650 IF (L < 1) THEN L = 1
660 IF (L > 10) THEN L = 10
670 GOSUB 5000
680 IF (H > 10) THEN GOTO 900
690 GOTO 500
900 GOSUB 7000 : PRINT "DAM, YOU BEAT ME."
998 END
999 REM PUT THING
1000 GOSUB 8000 : I = INT(M*V+1) : IF I=1 THEN C=C+1
1010 GOSUB 8000 : X = M * 40 :PRINT TAB(X);A$(I)
1020 RETURN
1999 REM SCORE RESULT
2000 INPUT "HOW MANY CATS DID YOU COUNT "; U
2010 PRINT
2020 IF C <> U THEN GOTO 2050
2030 S=S+1 : PRINT "YAY! YOU GOT IT!"
2035 IF L = 10 THEN H = H + 1
2040 GOTO 2060
2050 W=W+1 : PRINT "ER, NO."
2055 Z = U : IF Z > 10 THEN Z = 10
2056 FOR D = 1 TO Z : GOSUB 8000 : NEXT D
2060 T = S+W : PRINT "YOUR SCORE IS ";S;"/"T"
2070 RETURN
3000 REM PUT SOMETHING
3010 GOSUB 8000 : IF M < T THEN GOTO 3099
3020 GOSUB 1000
3099 RETURN
4000 REM LOAD ANIMALS
4010 DIM A$(10) : A$(1) = "CAT" : A$(2) = "DOG" : A$(3) = "BADGER"
4020 A$(4) = "SNAKE" : A$(5) = "BAT" : A$(6) = "MAT" : A$(7) = "HAT"
4030 A$(8) = "ELEPHANT" : A$(9) = "RAT" : A$(10) = "CAR"
4040 RETURN
5000 RESTORE
5010 FOR N = 1 TO L
5020 READ V,T,R,E
5030 NEXT N
5040 RETURN
5100 DATA 2 , 0.8  , 80 , 60
5110 DATA 3 , 0.7  , 70 , 55
5120 DATA 4 , 0.6  , 65 , 50
5130 DATA 5 , 0.5  , 62 , 45
5140 DATA 6 , 0.4  , 60 , 42
5150 DATA 7 , 0.4  , 55 , 40
5160 DATA 8 , 0.25 , 52 , 38
5170 DATA 9 , 0.2  , 50 , 35
5180 DATA 10, 0.15 , 45 , 32
5190 DATA 10, 0.1  , 48 , 30
6000 PRINT "                                  ."
6010 PRINT "                          ,*:. .;SC,"
6020 PRINT "                          ;CCC#CCCC+"
6030 PRINT "                           SCCCCCCCS"
6040 PRINT "                           :CCCCCC#:"
6050 PRINT "                         .,+CCCCCC*"
6060 PRINT "                   .:*%S#CCCCCCCCC#"
6070 PRINT "                 ,?#CCCCCCCCCCCCCCC."
6080 PRINT "                *CCCCCCCCCCCCCCCCC+"
6090 PRINT "               ?CCCCCCCCCCCCCCCC%."
6100 PRINT "              *CCCCCCCCCCCCCCCC?"
6110 PRINT "             ,CCCCCCCCCCCCCCCCC."
6120 PRINT "             ?CCCCCCCCCCCC##CC#"
6130 PRINT "            :CCCCCCCCCCCC;  *CC."
6140 PRINT "          ,?CCCCCCCCCCCC:   .#C%."
6150 PRINT "      .:*SCC#S#CCCCCCCCCS%,  .%CC:"
6160 PRINT "    :%C#%*:.   ,,,,,,,,,,,.    .:."
6170 PRINT "    #C*"
6180 PRINT "    :%%,  CATS GAME BY RUE MOHR"
6190 PRINT "      ..   V2 2025"
6200 PRINT
6230 RETURN
7000 REM CLEARSCREEN ABSTRACTION
7005 REM PRINT CHR$(27);"[2J";
7010 CLS
7020 RETURN
8000 REM RANDOM ABSTRACTION
8005 REM M = RND(1000)/1001
8010 M = RND(1)
8020 RETURN
